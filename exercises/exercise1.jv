// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

// 1. This Jayvee model describes a pipeline 
// from a CSV file in the web 
// to a SQLite file sink.

pipeline AirportsPipeline {

	AirportsExtractor
		->AirportsTextFileInterpreter
		->AirportsCSVInterpreter 
	   	-> AirportsTableInterpreter
		-> AirportsLoader;

	block AirportsExtractor oftype HttpExtractor {
		url: "https://opendata.rhein-kreis-neuss.de/api/v2/catalog/datasets/rhein-kreis-neuss-flughafen-weltweit/exports/csv";
	}

	block AirportsTextFileInterpreter oftype TextFileInterpreter { }

	block AirportsCSVInterpreter oftype CSVInterpreter {
		delimiter: ';';
	}
 
	block AirportsTableInterpreter oftype TableInterpreter {
		header: true;
		columns: [

			"column_1" oftype integer,
			"column_2" oftype text,
			"column_3" oftype text,
			"column_4" oftype text,
			"column_5" oftype AirportCodeOr0,
			"column_6" oftype text,
			"column_7" oftype decimal,
			"column_8" oftype decimal,
			"column_9" oftype integer,
			"column_10" oftype decimal,
			"column_11" oftype AbbrevationOfLength1,
			"column_12" oftype text,
			"geo_punkt" oftype text
		];
	}

	constraint LengthExactlyOne on text:
	value.length == 1;

	constraint OnlyCapitalLettersAndDigits on text:
        value matches /^[A-Z0-9]*$/;

	valuetype AirportCodeOr0 oftype text {
	constraints: [
	    OnlyCapitalLettersAndDigits
	    ];
	}
	
	valuetype AbbrevationOfLength1 oftype text {
        constraints: [
            LengthExactlyOne,
            OnlyCapitalLettersAndDigits
            ];
	}

	block AirportsLoader oftype SQLiteLoader {
		table: "airports";
		file: "./airports.sqlite";
	}
 
}
